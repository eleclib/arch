
(defconst forage-bush 59)
(defconst fact-population-cap 1)
(defconst fact-population 30)

(defconst current_pop 1)
(defconst cap_pop 2)

(defrule
    (true)
    =>
    (set-strategic-number sn-enable-new-building-system 1)
    (set-strategic-number sn-enable-patrol-attack 1)
    (set-strategic-number sn-initial-exploration-required 0)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-number-explore-groups 1)
    (set-strategic-number sn-cap-civilian-builders 200)
    (disable-self)
)

(defrule
    (true)
    =>
    (set-strategic-number sn-maximum-wood-drop-distance 30)
    (set-strategic-number sn-maximum-food-drop-distance 30)
    (set-strategic-number sn-maximum-hunt-drop-distance 30)
    (set-strategic-number sn-maximum-fish-boat-drop-distance 30)
    (set-strategic-number sn-maximum-gold-drop-distance 30)
    (set-strategic-number sn-maximum-stone-drop-distance 30)
    (disable-self)
)

(defrule
    (true)
    =>
    (set-strategic-number sn-food-gatherer-percentage 100)
    (disable-self)
)

(defrule
    (unit-type-count villager >= 17)
    =>
    (set-strategic-number sn-wood-gatherer-percentage 38)
    (set-strategic-number sn-food-gatherer-percentage 62)
    (disable-self)
)

(defrule
    (current-age == feudal-age)
    =>
    (set-strategic-number sn-wood-gatherer-percentage 42)
    (set-strategic-number sn-food-gatherer-percentage 48)
    (set-strategic-number sn-gold-gatherer-percentage 10)
    (disable-self)
)

;(defrule
    ;(true)
    ;=>
    ;(set-escrow-percentage wood 65)
    ;(set-escrow-percentage food 65)
    ;(set-escrow-percentage gold 65)
    ;(set-escrow-percentage stone 65)
    ;(disable-self)
;)

(defrule
    (current-age == feudal-age)
    =>
    (set-escrow-percentage wood 70)
    (set-escrow-percentage food 70)
    (set-escrow-percentage gold 70)
    (set-escrow-percentage stone 70)
    (disable-self)
)

(defrule
    (true)
    =>
    (research ri-loom)
)

(defrule
    (true)
    =>
    (research feudal-age)
)

(defrule
    (can-research-with-escrow castle-age)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research castle-age)
)

(defrule
    (can-research-with-escrow imperial-age)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research imperial-age)
)

(defrule
    (unit-type-count villager < 100)
    (can-train-with-escrow villager)
    =>
    (release-escrow food)
    (train villager)
)

(defrule
    (true)
    =>
    (train spearman)
)

(defrule
    (true)
    =>
    (train skirmisher)
)

(defrule
    (true)
    =>
    (train scout-cavalry)
)

(defrule
    (player-in-game any-ally)
    (unit-type-count trade-cart < 20)
    =>
    (train trade-cart)
)

(defrule
    (true)
    =>
    (train knight)
    (train eagle-warrior)
)

(defrule
    (true)
    =>
    (train scorpion)
)

(defrule
    (true)
    =>
    (train mangonel)
)

(defrule
    (true)
    =>
    (train battering-ram)
)

(defrule
    (true)
    =>
    (train my-unique-unit)
)

(defrule
    (housing-headroom < 5)
    (up-pending-objects c: house < 3)
    =>
    (build house)
)

(defrule
    (or
        (up-gaia-type-count c: food > 2)
        (up-gaia-type-count c: forage-bush > 0)
    )
    (building-type-count mill < 1)
    (up-pending-objects c: mill < 1)
    =>
    (build mill)
)

(defrule
    (up-gaia-type-count c: wood > 25)
    (building-type-count lumber-camp < 1)
    (up-pending-objects c: lumber-camp < 1)
    =>
    (build lumber-camp)
)

(defrule
    (current-age == dark-age)
    (building-type-count lumber-camp > 0)
    (idle-farm-count < 1)
    (up-pending-objects c: farm < 1)
    =>
    (build farm)
)

(defrule
    (current-age == feudal-age)
    (or
        (building-type-count barracks > 0)
        (building-type-count farm < 20)
    )
    (idle-farm-count < 1)
    (up-pending-objects c: farm < 1)
    =>
    (build farm)
)

(defrule
    (current-age == castle-age)
    (or
        (building-type-count siege-workshop > 0)
        (building-type-count farm < 40)
    )
    (idle-farm-count < 1)
    (up-pending-objects c: farm < 1)
    =>
    (build farm)
)

(defrule
    (current-age == imperial-age)
    (idle-farm-count < 1)
    (up-pending-objects c: farm < 1)
    =>
    (build farm)
)
(defrule
    (building-type-count mill > 0)
    (building-type-count lumber-camp > 0)
    (building-type-count barracks < 1)
    (up-pending-objects c: barracks < 1)
    (can-build-with-escrow barracks)
    =>
    (release-escrow wood)
    (build barracks)
)

(defrule
    (building-type-count barracks > 0)
    (building-type-count archery-range < 1)
    (up-pending-objects c: archery-range < 1)
    (can-build-with-escrow archery-range)
    =>
    (release-escrow wood)
    (build archery-range)
)

(defrule
    (building-type-count barracks > 0)
    (building-type-count stable < 1)
    (up-pending-objects c: stable < 1)
    =>
    (release-escrow wood)
    (build stable)
)

(defrule
    (or
        (building-type-count archery-range > 0)
        (or
            (building-type-count stable > 0)
            (building-type-count watch-tower > 0)
        )
    )
    (building-type-count blacksmith < 1)
    (up-pending-objects c: blacksmith < 1)
    (can-build-with-escrow blacksmith)
    =>
    (release-escrow wood)
    (build blacksmith)
)

(defrule
    (building-type-count market < 1)
    (up-pending-objects c: market < 1)
    (can-build-with-escrow market)
    =>
    (release-escrow wood)
    (build market)
)

(defrule
    (dropsite-min-distance gold >= 5)
    (dropsite-min-distance gold != -1)
    (dropsite-min-distance gold != 255)
    (up-pending-objects c: mining-camp < 1)
    (can-build-with-escrow mining-camp)
    =>
    (up-modify-sn sn-mining-camp-max-distance c:+ 5)
    (release-escrow wood)
    (build mining-camp)
)

(defrule
    (dropsite-min-distance stone >= 5)
    (dropsite-min-distance stone != -1)
    (dropsite-min-distance stone != 255)
    (up-pending-objects c: mining-camp < 1)
    (can-build-with-escrow mining-camp)
    =>
    (up-modify-sn sn-mining-camp-max-distance c:+ 5)
    (release-escrow wood)
    (build mining-camp)
)

(defrule
    (dropsite-min-distance wood >= 5)
    (dropsite-min-distance wood != -1)
    (dropsite-min-distance wood != 255)
    (up-pending-objects c: lumber-camp < 1)
    (can-build-with-escrow lumber-camp)
    =>
    (up-modify-sn sn-lumber-camp-max-distance c:+ 2)
    (release-escrow wood)
    (build lumber-camp)
)

(defrule
    (true)
    =>
    (build castle)
)

(defrule
    (current-age == castle-age)
    (building-type-count barracks < 5)
    (up-pending-objects c: barracks < 1)
    (can-build-with-escrow barracks)
    =>
    (release-escrow wood)
    (build barracks)
)

(defrule
    (current-age == castle-age)
    (building-type-count archery-range < 5)
    (up-pending-objects c: archery-range < 1)
    (can-build-with-escrow archery-range)
    =>
    (release-escrow wood)
    (build archery-range)
)

(defrule
    (current-age == castle-age)
    (building-type-count stable < 5)
    (up-pending-objects c: stable < 1)
    =>
    (release-escrow wood)
    (build stable)
)

(defrule
    (building-type-count siege-workshop < 5)
    (up-pending-objects c: siege-workshop < 1)
    (can-build-with-escrow siege-workshop)
    =>
    (release-escrow wood)
    (build siege-workshop)
)

(defrule
    (building-type-count monastery < 5)
    (up-pending-objects c: monastery < 1)
    (can-build-with-escrow monastery)
    =>
    (release-escrow wood)
    (build monastery)
)

(defrule
    (building-type-count university < 1)
    (up-pending-objects c: university < 1)
    (can-build-with-escrow university)
    =>
    (release-escrow wood)
    (build university)
)

(defrule
    (true)
    =>
    (up-get-fact fact-population-cap 0 cap_pop)
    (up-get-fact fact-population 0 current_pop)
)

(defrule
    (or
        (military-population > 30)
        (up-compare-goal current_pop g:>= cap_pop)
    )
    (up-enemy-buildings-in-town < 1)
    =>
    (up-modify-sn sn-maximum-town-size c:+ 5)
)

(defrule
    (military-population < 15)
    =>
    (set-strategic-number sn-maximum-town-size 20)
)